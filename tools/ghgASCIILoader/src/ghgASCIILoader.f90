program ghgASCIILoader
  
    use fileIOModule
    implicit none
    real, allocatable   :: moleFrac(:), time(:)
    integer, allocatable    :: year(:)
    character(300)          :: inputFile, ghgName
    integer                 :: timesteps, k
    integer, dimension(12)  :: daysInMonth = [ 31,28,31,30,31,30,31,31,30,31,30,31 ]
    real, parameter         :: fillValue = 1.E38

    call processArguments
    open(unit = 10, file = inputFile, form = 'formatted', status = 'old', action = 'read')
    call initializeData
    call loadData
    
    ! Make the timestamps. Make same day/hour/minute as the file used for CMIP6
    do k = 1,timesteps
      time(k) = buildTimestep(12, 0, 183, year(k)) !18500702.5
    end do

    call exportVariable(trim(ghgName), moleFrac, time)
    close(unit = 10)

contains

    subroutine processArguments
        implicit none
        if (iargc() .ne. 2) then
            stop ('Usage is: ghgASCIILoader [ghg name, e.g. CO2] [input file]')
        endif
        call getarg(1, ghgName)
        call getarg(2, inputFile)
    end subroutine processArguments

    subroutine initializeData
        implicit none
        integer                 :: fileSize
        inquire(file = inputFile, size = fileSize)
        timesteps = (fileSize + 1) / 13; ! this assumes you have only 'year GHGconc' in file, no header, one year per line.
        print*,'Number of timesteps is estimated to be',timesteps
        allocate(year(timesteps))
        allocate(moleFrac(timesteps))
        allocate(time(timesteps))
    end subroutine initializeData

    subroutine loadData()
        implicit none
        integer                 :: i
        character(*), parameter :: format = '(I4, 2X, F5.2)'

        do i = 1, timesteps
            read(unit = 10, fmt = format) year(i), moleFrac(i)
        enddo
    end subroutine loadData

    real function buildTimestep(hour, minute, dayIn, year)
        implicit none
        integer, intent(in) :: hour, minute, dayIn, year
        real                :: time, day
        integer             :: i, month

        day = dayIn
        time = (real(hour) * 60 + real(minute)) / 1440  ! 1440 minutes in a day

        month = 0
        do i = 1, 12
            if (day > daysInMonth(i)) then
                day = day - daysInMonth(i)
            else
                month = i
                exit
            endif
        enddo

        buildTimestep = year * 10000 + month * 100 + day + time
        
    end function buildTimestep

    real function charToReal(input)
        implicit none
        character(len=*), intent(in)    :: input    !< Char input
        read(input,*) charToReal
    end function charToReal

    subroutine exportVariable(label, variable, timestamps)
        implicit none
        character(*), intent(in)    :: label
        real, intent(in)            :: variable(:), timestamps(:)
        integer                     :: fileId, varId, timeDimId
        character(200)              :: filename, units, title, name

        ! Make the filename
        filename = label // '.nc'

        ! If the file doesn't already exist, then initialize the file
        if (.not.fileExists(filename)) then
            ! Create the variable file
            fileId = ncCreate(filename, NF90_NETCDF4)

            ! Populate the variable with attribute content
            select case(label)
            case('CO2')
                units = 'ppmv'
                title = 'Atmospheric Carbon Dioxide Concentration'
                name = 'CO2'
            case('CH4')
                units = 'ppmv'
                title = 'Atmospheric Methane Concentration'
                name = 'CH4'
            case default
                stop ('Unrecognized label')
            end select

            ! Define file attributes
            call ncPutAtt(fileId, nf90_global, 'title', charValues = 'Generated by tools/ghgASCIILoader')

            ! Define the time dimension
            timeDimId = ncDefDim(fileId, 'time', size(timestamps))
            varid = ncDefVar(fileId, 'time', nf90_double, [timeDimId])
            call ncPutAtt(fileId, varId, 'standard_name', charValues = 'time')
            call ncPutAtt(fileId, varId, 'units', charValues = 'day as YYYYMMDD.FFFF')
            call ncPutAtt(fileId, varId, 'calendar', charValues = 'proleptic_gregorian')
            call ncEndDef(fileId)
            call ncPutDimValues(fileId, 'time', timestamps, count = [size(timestamps)])

            call ncRedef(fileId)

            ! Define the variable
            varid = ncDefVar(fileId, label, nf90_double, [timeDimId])
            call ncPutAtt(fileId, varId, '_FillValue', realValues = fillValue)
            call ncPutAtt(fileId, varId, 'title', charValues = title)
            call ncPutAtt(fileId, varId, 'units', charValues = units)
            call ncEndDef(fileId)

        else
            fileId = ncOpen(filename, nf90_write)
        endif

        ! Put in data
        call ncPutVar(fileId, label, realValues = variable, start = [1], count = [size(variable)])

        call ncClose(fileId)
    end subroutine exportVariable

    logical function fileExists(filename)
        character(*), intent(in) :: filename
        inquire(file = filename, exist = fileExists)
    end function fileExists

end program
